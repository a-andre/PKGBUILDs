# U-Boot: NanoPC-T4

buildarch=8

pkgname=uboot-nanopct4
pkgver=2021.01
pkgrel=1
pkgdesc="U-Boot for NanoPC-T4"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
backup=('boot/boot.txt' 'boot/boot.scr')
makedepends=('bc' 'git' 'dtc')
install=${pkgname}.install
_commit_rkbin=0bb1c512492386a72a3a0b5a0e18e49c636577b9
source=("https://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        "git+https://github.com/rockchip-linux/rkbin.git#commit=$_commit_rkbin"
        "boot.txt"
        "mkscr")
sha256sums=('b407e1510a74e863b8b5cb42a24625344f0e0c2fc7582d8c866bd899367d0454'
            'SKIP'
            'd72d3d297b13dd30269810c0787fef6fc487a6672c717a9de8cc00bac2be2be7'
            'a4fc8b6b92bc364d6542670d294aa618a8501fb8729f415cc0a3eed776ef0c8e')

prepare() {
  cd ${srcdir}/u-boot-${pkgver}
  cp ../rkbin/bin/rk33/rk3399_bl31_v1.35.elf ./bl31.elf

  cd ${srcdir}/u-boot-${pkgver}/configs
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> nanopc-t4-rk3399_defconfig
}

build() {
  cd ${srcdir}/u-boot-${pkgver}

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  make nanopc-t4-rk3399_defconfig
  make EXTRAVERSION=-${pkgrel}
  make EXTRAVERSION=-${pkgrel} u-boot.itb
}

package() {
  cd u-boot-${pkgver}

  mkdir -p "${pkgdir}/boot"

  tools/mkimage -n rk3399 -T rksd -d ./tpl/u-boot-tpl.bin "${pkgdir}/boot/rksd_loader.img"
  cat ./spl/u-boot-spl.bin >> "${pkgdir}/boot/rksd_loader.img"

  cp ./u-boot.itb "${pkgdir}/boot/u-boot.itb"

  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d ../boot.txt "${pkgdir}/boot/boot.scr"
  cp ../{boot.txt,mkscr} "${pkgdir}"/boot
}
